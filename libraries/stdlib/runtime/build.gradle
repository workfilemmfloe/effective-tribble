description 'Kotlin Runtime (deprecated, use kotlin-stdlib artifact instead)'

apply plugin: 'kotlin'

// TODO: Drop this project entirely

configureJvm6Project(project)

configurations {
    builtins
}

dependencies {
    compile group: 'org.jetbrains', name: 'annotations', version:'13.0'
    builtins project(':core:builtins')
}

sourceSets {
    main {
        if(!System.properties.'idea.active') {
            java {
                srcDir "${rootDir}/core/builtins/src"
                srcDir "${rootDir}/libraries/stdlib/jvm/runtime"
            }
        }
    }

}

jar {
    dependsOn(configurations.builtins)
    manifestAttributes(manifest, project, 'Main')
    from {
        zipTree(configurations.builtins.asPath)
    }
}

sourcesJar {
    classifier = 'sources'
    from sourceSets.main.kotlin
    from "${rootDir}/core/builtins/native"
}

artifacts {
    archives sourcesJar
    archives javadocJar
}

compileKotlin {
    kotlinOptions {
        freeCompilerArgs = [
                "-Xallow-kotlin-package",
                "-Xnormalize-constructor-calls=enable",
                "-Xmulti-platform",
                "-cp", "${rootDir}/dist/builtins",
                "-module-name", project.name
        ]
    }
}
