apply plugin: 'kotlin'

configureJvmProject(project)
configurePublishing(project)

repositories {
    mavenLocal()
    jcenter()
    maven { url 'http://repository.jetbrains.com/utils' }
}

dependencies {
    compile project(':kotlin-gradle-plugin-api')

    compile "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    compileOnly "org.jetbrains.kotlin:kotlin-compiler-embeddable:$kotlin_version"

    compileOnly 'org.jetbrains.kotlin:gradle-api:1.6'
}

def originalSrc = "$kotlin_root/plugins/allopen/allopen-cli/src"
def targetSrc = file("$buildDir/allopen-target-src")

task preprocessSources(type: Copy) {
    from originalSrc
    into targetSrc
    filter { it.replaceAll('(?<!\\.)com\\.intellij', 'org.jetbrains.kotlin.com.intellij') }
}

sourceSets.main.java.srcDirs += targetSrc

compileKotlin.dependsOn preprocessSources

jar {
    from(targetSrc) { include("META-INF/**") }
}

pluginBundle {
    plugins {
        kotlinAllopenPlugin {
            id = 'org.jetbrains.kotlin.plugin.allopen'
            description = displayName = 'Kotlin All Open compiler plugin'
        }
        kotlinSpringPlugin {
            id = 'org.jetbrains.kotlin.plugin.spring'
            description = displayName = 'Kotlin Spring compiler plugin'
        }
    }
}